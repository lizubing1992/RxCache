apply plugin: 'com.android.library'
apply plugin: 'android-apt'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"
  }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def siteUrl = 'https://github.com/lizubing1992/RxCache'   // 项目的主页
def gitUrl = 'https://github.com/lizubing1992/RxCache.git'   // Git仓库的url

version = "1.3"
group = "com.lizubing1992"

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.1"

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 23
    versionCode 4
    versionName "1.3"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  lintOptions {
    abortOnError false
  }
}

bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")
  pkg {
    repo = 'licache'//自己bintray创建仓库名字
    name = 'licache'//Library的名字
    websiteUrl = siteUrl
    vcsUrl = gitUrl
    licenses = ['MIT']//不能随便写，只能是仓库创建时选择的license type
    userOrg = 'licache' //自己bintray创建的organization名称
    publish = true // 是否是公开项目。

    version {
      name = '1.3'
      desc = 'High integrated development framework for Android applications.'
      released  = new Date()
      vcsTag = 'v1.3'
      attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
    }
  }
  configurations = ['archives']
}


install {
  repositories.mavenInstaller {
    // This generates POM.xml with proper parameters
    pom {
      project {
        packaging 'aar'
        // Add your description here
        name 'RxCache Android'
        description 'High integrated development framework for Android applications.'
        url siteUrl
        // Set your license
        licenses {
          license {
            name 'MIT'
            url 'https://raw.githubusercontent.com/minggo620/Pluto-Android/master/LICENSE'
          }
        }
        developers {
          developer {
            id 'lizubing1992'        //填写bintray或者github的用户名
            name 'lizubing1992'         //姓名，可以是中文
            email 'lizubing2016@gmail.com'
          }
        }
        scm {
          connection gitUrl
          developerConnection gitUrl
          url siteUrl
        }
      }
    }
  }
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}
task javadoc(type: Javadoc) {
  options.encoding = "utf-8"
  options.addStringOption('Xdoclint:none', '-quiet')
  options.addStringOption('encoding', 'UTF-8')
  options.addStringOption('charSet', 'UTF-8')
  failOnError false //必须添加以免出错
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}
artifacts {
  archives javadocJar
  archives sourcesJar
}




dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile project(':runtime')
  apt project(':compiler')
  compile project(':api')
  compile project(':gson')
  testCompile 'junit:junit:4.12'

  apt "com.google.dagger:dagger-compiler:2.7"
  compile "com.google.dagger:dagger:2.7"
  provided "org.glassfish:javax.annotation:10.0-b28"
}


/*configurations {
    compile.exclude module: 'guava'
}*/
